operation Collection equalsIgnoreOrder(other : Collection) {
	assertTrue(self.containsAll(other));
	assertEquals(self.size(), other.size());
}

@test
operation testParallelSelect() {
	var data := Sequence {0..9};
	var selected = data.parallelSelect(n | (n > 3 and n <= 7) or n == 2);
	var expected := Sequence {2, 4, 5, 6, 7};
	selected.equalsIgnoreOrder(expected);
	selected = data.parallelSelect(n | n > 10);
	assertEquals(Sequence{}, selected);
	selected = data.parallelSelect(n | n < 10);
	selected.equalsIgnoreOrder(data);
}

@test
operation testParallelSelectOrdered() {
	var data := Sequence {0..9};
	var selected = data.parallelSelectOrdered(n | (n > 3 and n <= 7) or n == 2);
	var expected := Sequence {2, 4, 5, 6, 7};
	assertEquals(expected, selected);
	selected = data.parallelSelectOrdered(n | n > 10);
	assertEquals(Sequence{}, selected);
	selected = data.parallelSelectOrdered(n | n < 10);
	assertEquals(data, selected);
}

@test
operation testParallelSelectOne() {
	var data := Sequence {0..9};
	var selected = data.parallelSelectOne(n | n < 3 and n > 1);
	assertEquals(2, selected);
	selected = data.parallelSelectOne(n | n == 8);
	assertEquals(8, selected);
	selected = data.parallelSelectOne(n | n > 9);
	assertEquals(null, selected);
}

@test
operation testParallelReject() {
	var data := Sequence {0..9};
	var rejected = data.parallelRejectOrdered(n | n < 1 or n > 6 or n == 5 and n <> 9);
	var expected := Bag {1, 2, 3, 4, 6, 9};
	rejected.equalsIgnoreOrder(expected);
	rejected = data.parallelRejectOrdered(n | n > 10);
	rejected.equalsIgnoreOrder(data);
	rejected = data.parallelRejectOrdered(n | n < 10);
	rejected.equalsIgnoreOrder(Sequence{});
}

@test
operation testParallelRejectOrdered() {
	var data := Sequence {0..9};
	var rejected = data.parallelRejectOrdered(n | n < 1 or n > 6 or n == 5 and n <> 9);
	var expected := Sequence {1, 2, 3, 4, 6, 9};
	assertEquals(expected, rejected);
	rejected = data.parallelRejectOrdered(n | n > 10);
	assertEquals(data, rejected);
	rejected = data.parallelRejectOrdered(n | n < 10);
	assertEquals(Sequence{}, rejected);
}

@test
operation testParallelRejectOne() {
	var data := Sequence {0..9};
	var rejected = data.parallelRejectOne(n | n > 3);
	data.remove(4);
	assertEquals(data, rejected);
	data.add(4);
	rejected = data.parallelRejectOne(n | n > 9);
	assertEquals(data, rejected);
}

@test
operation testParallelExists() {
	var data := Sequence {0..9};
	var exists = data.parallelExists(n | n > 3 and n < 8);
	assertTrue(exists);
	exists = data.parallelExists(n | n < 0);
	assertFalse(exists);
}

@test
operation testParallelOne() {
	var data := Sequence {0..9};
	var one = data.parallelOne(n | n > 3 and n < 7);
	assertFalse(one);
	one = data.parallelOne(n | n == 2 or (n > 1 and n < 3));
	assertTrue(one);
	one = data.parallelOne(n | n < 0);
	assertFalse(one);
}

@test
operation testParallelNone() {
	var data := Sequence {0..9};
	var none = data.parallelNone(n | n == null);
	assertTrue(none);
	none = data.parallelNone(n | n > 7);
	assertFalse(none);
}

@test
operation testParallelForAll() {
	var data := Sequence {0..9};
	var forAll = data.parallelForAll(n | n > -1 and n <= 10);
	assertTrue(forAll);
	forAll = data.parallelForAll(n | n < 9);
	assertFalse(forAll);
}

@test
operation testParallelCollect() {
	var data := Sequence {0..9};
	var mapped = data.parallelCollect(n | n * n);
	var expected := Bag {0, 1, 4, 9, 16, 25, 36, 49, 64, 81};
	mapped.equalsIgnoreOrder(expected);
	data = Sequence {12, 10, null, false, true, "str", 3.14, 8};
	mapped = data.parallelCollect(n | 10);
	expected := Bag {10, 10, 10, 10, 10, 10, 10, 10};
	mapped.equalsIgnoreOrder(expected);
}

@test
operation testParallelCollectOrdered() {
	var data := Sequence {0..9};
	var mapped = data.parallelCollectOrdered(n | n * n);
	var expected := Sequence {0, 1, 4, 9, 16, 25, 36, 49, 64, 81};
	assertEquals(expected, mapped);
	data = Sequence {12, 10, null, false, true, "str", 3.14, 8};
	mapped = data.parallelCollectOrdered(n | 10);
	expected := Sequence {10, 10, 10, 10, 10, 10, 10, 10};
	assertEquals(expected, mapped);
}

@test
operation testParallelSortBy() {
	var data = Bag {"a", "", true, false, "          __a_ ", "---", 22, 666666};
	var expected = Sequence {"", "a", 22, "---", true, false, 666666, "          __a_ "};
	var sorted = data.parallelSortBy(d | d.asString().length());
	sorted.equalsIgnoreOrder(expected);
}

@test
operation testParallelSortByOrdered() {
	var data = Bag {"a", "", true, false, "          __a_ ", "---", 22, 666666};
	var expected = Sequence {"", "a", 22, "---", true, false, 666666, "          __a_ "};
	var sorted = data.parallelSortByOrdered(d | d.asString().length());
	assertEquals(expected, sorted);
}

@test
operation testParallelMapBy() {
	var data = Bag {"", 1, true, false, 98, 3, 55, 0, 4443, 768, 12024, "eol", 640, "four", "null"};
	var expected : new Map;
	expected.put(0, Sequence{""});
	expected.put(1, Sequence{1, 3, 0});
	expected.put(2, Sequence{98, 55});
	expected.put(3, Sequence{768, "eol", 640});
	expected.put(4, Sequence{true, 4443, "four", "null"});
	expected.put(5, Sequence{false, 12024});
	var actual = data.parallelMapBy(n | n.toString().length());
	assertEquals(expected, actual);
}

/*@test
operation testParallelAggregate() {
	var data = Bag {-127, -64, -2, 0, "", 32768};
	var expected = Map {4 = -127, 3 = -64, 2 = -2, 1 = 0, 0 = "", 5 = 32768};
	var actual = data.parallelAggregate(n | n.asString().length(), n);
	assertEquals(expected, actual);
}*/
